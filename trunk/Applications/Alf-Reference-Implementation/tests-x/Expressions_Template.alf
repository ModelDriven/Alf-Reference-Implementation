/**
 * Test expressions involving templates.
 **/
active class Expressions_Template {

    public receive signal Continue { }

    public activity A<T specializes B>(in x: T): Integer { 
      return x.i;
    }
    
    public class B {
      public i: Integer;
      @Create public B(in i: Integer) { this.i = i; }
    }
    
    public class C<T> {
        p: T;
        
        @Create public C(in p: T) { this.p = p; }
        r(): T { return this.p; }
        
        activity E(in x: T): T { return x; }
    }
    
    public active class H<T> {
        public a: T;
        public b: T;
        public t: Expressions_Template;
        @Create public H(in a: T, in t: Expressions_Template) { 
          this.a = a; 
          this.t = t;
        }
    } do {
        $(this.b = new C<T>(this.a).r());
        this.t.Continue();
    }
    
    public test(
      out i: Integer, out j: Integer, out h: H<B>,
      out s: Set<Integer>, out list: List<Integer>) {
      
      b = new B(1);
      
      i = A<B>(b);
      j = C<B>.E(b).i;
      h = new H<B>(b, this);      

      s = new Set<Integer>(null);
      s.add(1);
      
      list = List<Integer>{1, 2, 3};
    }
    
} do {
  this.test(i, j, h, s, list);
  accept (Continue);
  
  AssertEqual("i", i, 1);
  AssertEqual("j", j, 1);
  AssertEqual("h.b.i", h.b.i, 1);
  
  AssertList("s", s.toSequence(), 1);
  AssertList("list", list.toSequence(), Integer[]{1,2,3});
}
