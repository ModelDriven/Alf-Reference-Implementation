namespace 'Property Management'::'Service Model'::
                           'Property Management Service Implementation';

private import 'Property Management'::'Data Model'::Properties::*;   
                                                           // See Note 1
private import 'Property Management'::'Message Model'::*;

/** Create a reply message for a given message ID and property object */
activity 'create reply'(in requestId: String, in property: Property):
                                   'Property Management Success Reply' {

  propertyData = new 'Property Data' (                     // See Note 2
    'property identifier'       => property.identifier,
    'property name'             => property.name,
    'property status'           => property.status,
    'property acquisition data' => property.'acquisition data',
    'property disposition data' => property.'disposition data' );

  if (property.location -> notEmpty()) {
    propertyData.'property location' 
      = new 'Location Data' (
          'location identifier' => property.location.identifier,
          'location address'    => property.location.address );
  }

  if (property instanceof 'Personal Property') {
    propertyData.'property serial number' 
      = (('Personal Property')property).'serial number';
  } else {
    propertyData.'property size' = (('Real Property')property).size;
  }

  return new 'Property Management Success Reply' (
    identifier           => requestId + "/reply",
    'request identifier' => requestId,
    property             => propertyData );
}
