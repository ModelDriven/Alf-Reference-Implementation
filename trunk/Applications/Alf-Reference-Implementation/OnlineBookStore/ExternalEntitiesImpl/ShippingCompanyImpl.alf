namespace ExternalEntitiesImpl;

active class ShippingCompanyImpl specializes ShippingCompany {

  private companyName: String = "Shipping Express";
  private trackingNumber: Integer = 1;
  
} do {

  while (true) {

    accept (ShipmentReadyForPickup);
    
    //@isolated
    {
      shipment = Shipping::Shipment->select selected (
        selected.timePrepared->notEmpty() && selected.timePickedUp->isEmpty() &&
        (selected.shippingCompany->isEmpty() || selected.shippingCompany == this.companyName)
      )[1];
      
      title = "SHIPPING COMPANY: " + this.companyName;
      
      if (shipment.shippingCompany->isEmpty()) {
        trackingNumber = IntegerFunctions::ToString(this.trackingNumber++);
        SendMessage(title, "Tracking number assigned: " + trackingNumber);
        
        shipment.TrackingNumberAssigned(
          shippingCompany => this.companyName, 
          trackingNumber => trackingNumber);
      } else {
        trackingNumber = shipment.trackingNumber;
      }
      
      SendMessage(title, "Shipment " + trackingNumber + " picked up.");      
      shipment.PickedUp();
      
      SendMessage(title, "Shipment " + trackingNumber + " delivered.");
      shipment.DeliveryConfirmed(
        timeDelivered => TIM::current_date()
      );
      
    }
    
  }
  
}
