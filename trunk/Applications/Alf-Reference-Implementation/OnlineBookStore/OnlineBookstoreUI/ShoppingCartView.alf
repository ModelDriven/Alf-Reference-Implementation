namespace OnlineBookstoreUI;

private import Ordering::*;

active class ShoppingCartView specializes View {

  @Create
  public ShoppingCartView (
    in controller: ShoppingCartController) {
    super(
      title => "Shopping Cart",
      prompt => "Select a quantity to change, c to check out, " +
                "e to empty, h for home or x to exit",
      controller => controller
    );
  }
  
  public getController(): ShoppingCartController {
    return (ShoppingCartController)this.controller;
  }

  public display(): String[*] sequence {
    choices = String[]{"c", "e", "h", "x"};
    i = 1;
    controller = (ShoppingCartController)this.controller;
    for (selection in controller.cart.'includes selection') {
      choice = IntegerFunctions::ToString(i++);
      choices->add(choice);
      product = selection.'is selection of';
      WriteLine(choice + ". " + product.productName + 
        " Quantity: " + IntegerFunctions::ToString(selection.quantity) +
        " Amount: " + ToMoneyString(selection.selectionValue));
    }
    WriteLine("");
    WriteLine("Total amount: " + ToMoneyString(controller.cart.totalValue));
    return choices;
  }

  protected handle(in choice: String) {
    switch (choice) {
      case "x": this.getController().Exit();
      case "h": this.getController().Home();
      case "e": this.emptyCart();
      case "c": this.checkOut();
      default:  this.changeQuantity(IntegerFunctions.ToInteger(choice));
    }
  }
    
  private emptyCart() {
    if (Ask("Are you sure you want to empty your cart")) {
      this.getController().Cancel();
    } else {
      this.getController().ShowShoppingCart();
    }
  }
  
  private checkOut() {
    customerEmail = GetCustomerEmail();
    this.getController().CheckOut(customerEmail);
  }
  
  private changeQuantity(in choice: Integer) {
    quantity = GetQuantity("Enter new quantity:", 0);
    if (quantity->notEmpty()) {
      this.getController().ChangeQuantity(choice, quantity);
    } else {
      this.getController().ShowShoppingCart();
    }
  }
  
} do {
}