namespace OnlineBookstoreUI;

private import Ordering::*;

active class ShoppingCartView specializes View {

  @Create
  public ShoppingCartView (
    in controller: ShoppingCartController) {
    super(
      title => "Shopping Cart",
      prompt => "Select a quantity to change, c to check out, " +
                "e to empty, b for back, h for home or x to exit",
      controller => controller
    );
  }
  
  public getController(): ShoppingCartController {
    return (ShoppingCartController)this.controller;
  }

  public display(): String[*] sequence {
    choices = String[]{"c", "e", "h", "b", "x"};
    i = 1;
    controller = (ShoppingCartController)this.controller;
    cart = controller.getCart();
    for (selection in cart.'includes selection') {
      choice = IntegerFunctions::ToString(i++);
      choices->add(choice);
      product = selection.'is selection of';
      WriteLine(choice + ". " + product.productName + 
        " Quantity: " + IntegerFunctions::ToString(selection.quantity) +
        " Amount: " + ToMoneyString(selection.selectionValue));
    }
    WriteLine("");
    WriteLine("Total amount: " + ToMoneyString(cart.totalValue));
    return choices;
  }

  protected handle(in choice: String) {
    controller = (ShoppingCartController)this.controller;
    switch (choice) {
      case "x": controller.Exit();
      case "b": controller.Back();
      case "h": controller.Home();
      case "e": controller.Cancel(Ask("Are you sure you want to empty your cart"));
      case "c": controller.CheckOut(GetCustomerEmail());
      default:  controller.ChangeQuantity(
                  IntegerFunctions.ToInteger(choice), 
                  GetQuantity("Enter new quantity:", 0));
    }
  }
  
} do {
}