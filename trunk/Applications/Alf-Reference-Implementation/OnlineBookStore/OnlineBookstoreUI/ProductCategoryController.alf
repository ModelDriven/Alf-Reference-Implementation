namespace OnlineBookstoreUI;

private import ProductSpecification::*;

active class ProductCategoryController specializes Controller {

  public title: String;
  public products: Product[*] sequence;
  
  @Create
  public ProductCategoryController(
    in title: String,
    in category: ProductCategory
  ) {
    this.title = title + " > " + category.categoryName;
    this.products = category.classifies->select p (p.currentlyAvailable);
  }
  
  protected displayView() {
    new ProductCategoryView(this.title, this);
  }
  
  protected handleChoice(in choice: String): Controller[0..1] {
    switch (choice) {
      case "x": return null;
      case "h": return new HomeController();
      case "s": return new ShoppingCartController();
      default:
        return new ProductController(
          this.title,
          this.products->at(IntegerFunctions::ToInteger(choice))
        );
    }
  }
  
} do {
}
