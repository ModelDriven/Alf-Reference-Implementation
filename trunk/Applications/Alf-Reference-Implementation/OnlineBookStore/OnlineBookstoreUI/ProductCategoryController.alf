namespace OnlineBookstoreUI;

private import ProductSpecification::*;

active class ProductCategoryController specializes Controller {

  public title: String;
  public products: Product[*] sequence;
  
  @Create
  public ProductCategoryController(
    in title: String,
    in category: ProductCategory
  ) {
    this.title = title + " > " + category.categoryName;
    this.products = category.classifies->select p (p.currentlyAvailable);
  }
  
  public receive signal Home {
  }
  
  public receive signal ShowShoppingCart {
  }
  
  public receive signal ShowProduct {
    public choice: Integer;
  }
  
} do {

  accept (Display);
  new ProductCategoryView(this.title, this);
  
  accept (Exit) {
  } or accept (Home) {
    new HomeController().Display();
  } or accept (ShowShoppingCart) {
    new ShoppingCartController().Display();
  } or accept (event: ShowProduct) {
    new ProductController(this.title, this.products->at(event.choice)).Display();
  }
  
  this.view.destroy();
}
