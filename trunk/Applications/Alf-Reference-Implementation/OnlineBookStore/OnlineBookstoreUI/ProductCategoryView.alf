namespace OnlineBookstoreUI;

private import ProductSpecification::*;

active class ProductCategoryView specializes View {

  @Create
  public ProductCategoryView (
    in title: String,
    in controller: ProductCategoryController) {
    super(
      title => title,
      prompt => "Select a product, b for back, h for home, s for shopping cart or x to exit.",
      controller => controller
    );
  }
  
  protected display(): String[*] sequence {
    choices = String[]{"x", "b", "h", "s"};
    i = 1;
    for (product in ((ProductCategoryController)this.controller).products) {
      if (product.currentlyAvailable) {
        choice = IntegerFunctions::ToString(i++);
        choices->add(choice);
        WriteLine(choice + ". " + product.productName);
      }
    }    
    return choices;
  }
  
  protected handle(in choice: String){
    controller = (ProductCategoryController)this.controller;
    switch (choice) {
      case "x": controller.Exit();
      case "b": controller.Back();
      case "h": controller.Home();
      case "s": controller.ShowShoppingCart();
      default: controller.ShowProduct(IntegerFunctions::ToInteger(choice));
    }
  }
  
} do {
}
