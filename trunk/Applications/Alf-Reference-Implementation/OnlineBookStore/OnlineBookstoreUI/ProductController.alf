namespace OnlineBookstoreUI;

private import ProductSpecification::*;

active class ProductController specializes Controller {

  public title: String;
  public product: Product;
  public cartID: arbitrary_id[0..1];
  
  @Create
  public ProductController(
    in title: String,
    in product: Product,
    in cartID: arbitrary_id[0..1]
  ) {
    this.title = title;
    this.product = product;
    this.cartID = cartID;
  }
  
  public receive signal Home {
  }
  
  public receive signal AddProduct {
    public quantity: Count;
  }
  
} do {

  accept (Display);
  
  continue = true;
  do {
    new ProductView(this.title, this);
    
    accept (Exit) {
      continue = false;
    } or accept (Home) {
      new HomeController(this.cartID).Display();
      continue = false;
    } or accept (event: AddProduct) {
      if (event.quantity->notEmpty()) {
        new ShoppingCartController(this.cartID).AddProduct(
          this.product.productID, event.quantity);
        continue = false;
      }
    }
  } while (continue);
  
}
