namespace OnlineBookstoreUI;

private import ProductSpecification::*;
private import Ordering::*;
private import Shipping::*;

active class HomeController specializes Controller {

  public cartID: arbitrary_id[0..1]; 
  public topLevelCategories: ProductCategory[*] sequence =
    ProductCategory->select pc (pc.'is part of'->isEmpty());
    
  @Create
  public HomeController(in cartID: arbitrary_id[0..1]) {
    this.cartID = cartID;
  }
    
  public receive signal ShowCategory {
    public choice: Integer;
  }
  
  public receive signal ShowShoppingCart {
  }
       
} do {

  accept (Display);
  new HomeView(this);
  
  let category : ProductCategory = null;
  accept (event: ShowCategory) {
    category = this.topLevelCategories->at(event.choice);
    if (category.'is divided into'->isEmpty()) {
      new ProductCategoryController("Home", category, this.cartID).Display();
    } else {
      new SubcategoryController("Home", category, this.cartID).Display();
    }
  } or accept (ShowShoppingCart) {
    new ShoppingCartController(this.cartID).Display();
  } or accept (Exit) {
  }
  
  this.view.destroy();
  
}
