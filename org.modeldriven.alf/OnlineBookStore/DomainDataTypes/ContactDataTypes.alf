namespace DomainDataTypes;

public import Alf::Library::PrimitiveTypes::String as WebsiteURL;
public import Alf::Library::PrimitiveTypes::String as InternetEmailAddress;
public import Alf::Library::PrimitiveTypes::String as TelephoneNumber;

package ContactDataTypes {

  public datatype PersonalName {
    public title: String[0..1];
    public firstName: String;
    public middleName: String[0..1];
    public lastName: String;
    public suffix: String[0..1];
  }
  
  public datatype MailingAddress {
    public addressLine1: String;
    public addressLine2: String[0..1];
    public city: String [0..1];
    public stateOrProvince: String[0..1];
    public country: String[0..1];
    public postalCode: String[0..1];
  }
  
  public activity ToNameString(in name: PersonalName[0..1]): String {
    return name == null? "":
      FormatString(name.title, "", " ") +
      FormatString(name.firstName, "", " ") +
      FormatString(name.middleName, "", " ") +
      FormatString(name.lastName) +
      FormatString(name.suffix, " ", "");
  }
  
  public activity ToAddressString(in address: MailingAddress[0..1]): String {
    return address == null? "":
      FormatString(address.addressLine1, "", "\n") +
      FormatString(address.addressLine2, "", "\n") +
      FormatString(address.city, "", " ") +
      FormatString(address.stateOrProvince, "", " ") +
      FormatString(address.postalCode, "", "\n") +
      FormatString(address.country, "", "\n");
  }
  
  private activity FormatString(
  	in string: String[0..1], in prefix: String[0..1], in suffix: String[0..1]) : String {
  	return string == null? "": (prefix ?? "") + string + (suffix ?? "");
  }

}
