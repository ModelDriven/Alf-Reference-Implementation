namespace ExternalEntitiesImpl;

active class OnlineCustomerImpl specializes OnlineCustomer {

  public cartChannels: Map<arbitrary_id, PublishingChannel> = new();
  public customerChannels: Map<InternetEmailAddress, PublishingChannel> = new();
  
  public registerForCart(
    in listener: Listener, in cartID: arbitrary_id) {
    //@isolated
    {
    
    Register(this.cartChannels, listener, cartID);
    
    }
  }
  
  public unregisterForCart(
    in listener: Listener, in cartID: arbitrary_id) {
    //@isolated
    {
    
    this.cartChannels.get(cartID).unregister(listener);
    
    }
  }
  
  public registerForCustomer(
    in listener: Listener, in customerEmail: InternetEmailAddress) {
    //@isolated
    {
    
    Register(this.customerChannels, listener, customerEmail);
    
    }
  }

  public unregisterForCustomer(
    in listener: Listener, in customerEmail: InternetEmailAddress) {
    //@isolated
    {
    
    this.customerChannels.get(customerEmail).unregister(listener);
    
    }
  }
    
  private activity Register<Key>(
    in map: Map<Key, PublishingChannel>, 
    in listener: Listener,
    in key: Key) {
    channel = map.get(key);
    if (channel->isEmpty()) {
      channel = new PublishingChannel();
      map.put(key, channel);
    }
    channel.register(listener);
  }

  private activity SendCustomerMessage(
    in customerEmail: InternetEmailAddress,
    in message: String
  ) {    
    SendMessage("CUSTOMER: " + customerEmail, message);
  }
  
} do {

  while (true) {
  
    accept (cartUpdated: ShoppingCartUpdated) {
      this.cartChannels.get(cartUpdated.cartID).notify();
    } or accept (chargeDeclined: ChargeDeclined) {
      SendCustomerMessage(chargeDeclined.customerEmail, "CHARGE DECLINED");
      this.customerChannels.get(chargeDeclined.customerEmail).notify(chargeDeclined);
    } or accept (chargeApproved: ChargeApproved) {
      SendCustomerMessage(chargeApproved.customerEmail, "CHARGE APPROVED");
      this.customerChannels.get(chargeApproved.customerEmail).notify(chargeApproved);
    } or accept (orderReportedDelivered: OrderReportedDelivered) {
      SendCustomerMessage(orderReportedDelivered.customerEmail, "ORDER DELIVERED");
    }
  
  }

}