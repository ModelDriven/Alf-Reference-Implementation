namespace OnlineBookstoreUI;

private import ProductSpecification::*;

active class ProductController specializes Controller {

  public title: String;
  public product: Product;
  
  @Create
  public ProductController(
    in title: String,
    in product: Product
  ) {
    this.title = title;
    this.product = product;
  }
  
  public receive signal Home {
  }
  
  public receive signal AddProduct {
    public quantity: Count;
  }
  
} do {

  accept (Display);
  
  continue = true;
  do {
    new ProductView(this.title, this);
    
    accept (Exit) {
      continue = false;
    } or accept (Home) {
      new HomeController().Display();
      continue = false;
    } or accept (event: AddProduct) {
      if (event.quantity->notEmpty()) {
        new ShoppingCartController::addProduct(
          this.product.productID, event.quantity).Display();
        continue = false;
      }
    }
  } while (continue);
  
}
