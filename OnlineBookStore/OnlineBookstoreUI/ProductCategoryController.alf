namespace OnlineBookstoreUI;

private import ProductSpecification::*;

active class ProductCategoryController specializes Controller {

  public title: String;
  public products: Product[*] sequence;
  public cartID: arbitrary_id[0..1];
  
  @Create
  public ProductCategoryController(
    in title: String,
    in category: ProductCategory,
    in cartID: arbitrary_id[0..1]
  ) {
    this.title = title + " > " + category.categoryName;
    this.products = category.classifies->select p (p.currentlyAvailable);
    this.cartID = cartID;
  }
  
  public receive signal Home {
  }
  
  public receive signal ShowShoppingCart {
  }
  
  public receive signal ShowProduct {
    public choice: Integer;
  }
  
} do {

  accept (Display);
  new ProductCategoryView(this.title, this);
  
  accept (Exit) {
  } or accept (Home) {
    new HomeController(this.cartID).Display();
  } or accept (ShowShoppingCart) {
    new ShoppingCartController(this.cartID).Display();
  } or accept (event: ShowProduct) {
    new ProductController(this.title, this.products->at(event.choice), this.cartID).Display();
  }
  
  this.view.destroy();
}
