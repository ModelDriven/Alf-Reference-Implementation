/**
 * Test association-related expressions.
 **/
active class Expressions_Association {

    // Supporting declarations *************************************************
    
    class Person { }
  	class House { }
  	class Player { }
  	class Team { }
  	class Season { }
  	
  	assoc Owns {
  		owner: Person;
  		house: House;
  	}
  	
  	assoc Owns1 {
  		owner1: Person;
  		house1: House[*] ordered;
  	}
  	
  	assoc Roster {
  		player: Player;
  		team: Team;
  		season: Season;
    }
  	
	//**************************************************************************

	test(
	   out owner: Person, out house: House, 
	   out house_a: House, out house_b: House[0..1], 
	   out owner1_a: Person, out house1_a: House[*],
	   out owner1_b: Person, out house1_b: House[*], out house1_c: House[*]) 
	{
	  
	  jack = new Person();
	  t = new Team();
	  y = new Season();
	  
	  newHouse = new House();
	  oldHouse = new House();
	  
	  Roster::player(team=>t, season=>y);
	  Roster.player(t,y);
	  
	  Owns.createLink(jack, oldHouse);
	  Owns.createLink(owner=>jack, house=>oldHouse);
	  
	  house = jack.house;
	  owner = Owns::owner(oldHouse);
	  
	  Owns.createLink(oldHouse.owner, newHouse);
	  Owns.createLink(Owns.owner(oldHouse), newHouse);
	  
	  house_a = jack.house;
	  
      Owns.destroyLink(owner=>jack, house=>newHouse);
	  
	  house_b = jack.house;
	  
      Owns1.createLink(owner1=>jack, house1[1]=>oldHouse);
	  Owns1.createLink(owner1=>jack, house1[2]=>newHouse);
	  Owns1.createLink(owner1=>jack, house1[1]=>newHouse);
	  
	  house1_a = jack.house1;
      owner1_a = Owns1::owner1(newHouse);
	  
	  Owns1.createLink(owner1=>new Person(), house1[1]=>newHouse);
	  
      house1_b = jack.house1;
	  owner1_b = Owns1::owner1(newHouse);
	  
	  Owns1.createLink(owner1=>jack, house1[2]=>new House());
	  
	  house1_c = jack.house1;

	  Owns.clearAssoc(jack);
	  
	}

} do {
    this.test();
}